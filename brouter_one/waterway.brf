---model:btools.router.WaterwayModel
---context:global   # following code refers to global config

# user values

# my boat in meter
assign boat_height	1.5     # %boat_height% | Boat height in Meter | number
assign boat_width	2.2     # %boat_width% | Boat width in Meter | number
assign boat_length	10.0	# Boat height in Meter | (not used now)
assign boat_draft	1.2		# %boat_draft% | Boat draft in Meter | number
assign literHour    1.0		# %literHour% | liter per hour | number

# my boat is CEMT class (decimal)
assign boat_class   1

# my boat has max speed
assign  maxSpeed             18  # km/h (Caution: this is case sensitive internal parameter and different to maxspeed)
                                 # handle with care, this belongs to your local area and your boat
assign  speed_normal         10  # %speed_normal% | Speed in kmh | number
assign  flow_rate            0   # %flow_rate% | Flow in kmh | number

# global values

assign   validForBikes            0
assign   validForCars             0
assign   validForFoot             0
assign   validForVessels          1

#assign totalMass  = 1000     # unused at the moment xtotalMass% | Mass (in kg) of the bike + biker, for travel time computation | number
#assign bikerPower = 15000    # unused at the moment xbikerPower% | Average power (in W) provided by the biker, for travel time computation | number

assign   shortest_way             0   # %shortest_way% | find the direct way | boolean

assign   useCEMTonly              0   # %useCEMTonly% | use only CEMT, exclude unmarked like canal, fairway | boolean

#assign  vmax 		              20

assign   turnInstructionMode      1   # 0=none, 1=auto-choose, 2=locus-style, 3=osmand-style, 4=comment-style, 5=gpsies, 6=orux-style
assign   processUnusedTags        1   # lists all (incl. unused) key/value pairs in geojson

assign   cost1speed               20  # kmh
assign   inverseRouting           1
assign   showspeed                1   # show speed instead of elevation (geojson)

assign   waiting_lock 			  900    # in seconds, for one lock_gate
assign   waiting_bridge			  900    # in seconds
assign   around_time			  900    # in seconds, avoid obstacles

assign   notdefined               10000


# https://wiki.openstreetmap.org/wiki/Key:CEMT
# https://wiki.openstreetmap.org/wiki/Inland_navigation

# CEMT is available in this region
assign can_use_CEMT               true



---context:way   # following code refers to way-tags

assign control_draft
	switch  and CEMT=0 not maxdraft=          true 
	switch  and CEMT=0 not draft=             true 
	switch  and CEMT=0 not depth=             true
	switch  and CEMT=0 not maxdraught=        true
#    switch and not waterway= not maxdraft=   true 
#    switch and not waterway= not draft=      true 
#    switch and not waterway= not depth=      true 
#    switch and not waterway= not maxdraught= true 
	false

assign control_bridge_width
    if not seamark:bridge:clearance_width= then true
	else false

# no chance to get crossing points use node, see below	
#assign control_cable_height
#    if not seamark:cable_overhead:clearance_height= then true
#	else if not seamark:cable_overhead:clearance_height_safe= then true
#	else false

assign pass_lock 
	if seamark:type=lock_basin then (
		true
		)
	else false

assign this_class                       # len       width 	draught 	height
    if      CEMT=0       then 1         # used in NL for recreational boating fairways 
    else if CEMT=I       then 2         # 38,50 	5,05 	1,8-2,2 	4
    else if CEMT=II      then 3         # 50-55 	6,6 	2,5 	    4-5
    else if CEMT=III     then 4         # 67-80 	8,2 	2,5 	    4-5
    else if CEMT=IV      then 5         # 80-85 	9,5 	2,5 	    5,25-7
    else if CEMT=V       then 6         # 95-110 	11,4 	2,5-4,5 	5,25-7
    else if CEMT=Va      then 6         # 95-110 	11,4 	2,5-4,5 	5,25-7
    else if CEMT=Vb      then 7         # 172-185 	11,4 	2,5-4,5 	5,25-9,1
    else if CEMT=VI      then 8         # 95-110 	22,8 	2,5-4,5 	7-9,1
    else if CEMT=VIa     then 8         # 95-110 	22,8 	2,5-4,5 	7-9,1
    else if CEMT=VIb     then 9         # 185-195 	22,8 	2,5-4,5 	7-9,1
    else if CEMT=VIc     then 10        # 193-200 	34,2 	2,5-4,5 	9,1
    else if CEMT=VII     then 11        # 195/285 	34,2 	2,5-4,5 	9,1
    else
        0
	
assign	use_CEMT 
    if not CEMT= then (
        # if motorboat=no then false
        # else if greater this_class boat_class then true
        # else
		switch motorboat=no false
		switch ship=no false
		switch CEMT=0|I|II|III|IV|V|Va|Vb|VI|VIa|VIb|VIc|VII true	# make sure to drop classes you can't use
        false
    )
    else 
	    false
		
		  
assign boat_maxspeed =
  if ( and waterway=river reversedirection=yes ) then add speed_normal flow_rate 
  else if ( and waterway=river reversedirection= ) then sub speed_normal flow_rate
  else speed_normal


assign maxspeed_impl =
  switch CEMT=0                     10 # 6
  switch CEMT=I|II|III|IV           10
  switch CEMT=V|Va|Vb               10 # 12
  switch CEMT=VI|VIa|VIb|VIc|VII    15
  20
  
assign maxspeed_expl =
  switch maxspeed=  notdefined
  switch maxspeed=6 6
  switch maxspeed=9 9
  switch maxspeed=4.5 4.5
  switch maxspeed=5 5
  switch maxspeed=7 7
  switch maxspeed=8 8
  switch maxspeed=10 10
  switch maxspeed=12 12
  switch maxspeed=13 13
  switch maxspeed=13.5 13.5
  switch maxspeed=15 15
  switch maxspeed=16 16
  switch maxspeed=20 20
  switch maxspeed=25 25
  notdefined

assign maxspeed_defined 
  switch lesser maxspeed_expl notdefined maxspeed_expl maxspeed_impl 
  
assign maxspeed =
  min maxspeed_defined boat_maxspeed

assign turncost 0


assign initialcost 
    if waterway=weir                then around_time
    else if waterway=dam            then around_time
    else if waterway=fish_pass      then around_time
    else
    0



assign costfactor
    if  use_CEMT then (
      	1
    )
    else (
        if      waterway=               then 10000
        else if not useCEMTonly then 
			if seamark:type=fairway    then 1
			else if waterway=canal 			then 1
			else if waterway=river 			then 1
			else if waterway=safe_water		then 1
			else if waterway=fairway        then 1
			else if waterway=narrow         then 1
			else if seamark:type=recommended_track	then 1
			else if motorboat=yes           then 1
			else if ship=yes                then 1
			else   
				15000
		else
			15000	
    )


---context:node  # following code refers to node tags

assign control_node_draft
	switch not maxdraft=    true
	switch not depth=       true
	switch not water:depth= true
	false

assign control_bridge_fixed_height
    if seamark:bridge:category=fixed then  true 
	else false

assign control_bridge_opening_height
    if seamark:bridge:category=opening then  true 
	else false

assign control_node_bridge_width
    if not seamark:bridge:clearance_width= then true
	else false
	
assign control_node_cable_height
    if not seamark:cable_overhead:clearance_height= then true
	else false
	
assign initialcost 
	switch waterway=lock_gate  waiting_lock
	switch seamark:bridge:category=opening waiting_bridge
    switch waterway=waterfall  around_time
	0