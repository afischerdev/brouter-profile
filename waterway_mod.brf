---context:global   # following code refers to global config

assign   validForFoot             0   # helps to get a time variable, limit is foot speed, not variable
assign   validForBikes            1   # helps to get a time variable
assign   validForCars             0
assign   validForBoat             1   # this is a nice to have, does nothing

assign   shortest_way             1   # %shortest_way% | use the shortest way | boolean

assign   speed_normal             10  # %speed_normal% | Speed in kmh | number

# my boat has max speed
assign   maxSpeed                 18  # km/h (Caution: this is case sensitive internal parameter and different to maxspeed)
                                      # handle with care, this belongs to your local area and your boat

assign   processUnusedTags        0   # lists all (incl. unused) key/value pairs in geojson
assign   showspeed                1   # show speed instead of elevation (geojson)

# Turn instructions settings
assign turnInstructionMode          = 4     # Mode for the generated turn instructions | [0=none, 1=auto-choose, 2=locus-style, 3=osmand-style, 4=comment-style, 5=gpsies-style, 6=orux-style]
assign turnInstructionCatchingRange = 40    # Within this distance (in m) several turning instructions are combined into one and the turning angles are better approximated to the general direction | number
assign turnInstructionRoundabouts   = false # Set to "false" to avoid generating special turning instructions for roundabouts | boolean

assign   waiting_lock             900    # in seconds, for one lock_gate
assign   waiting_bridge           900    # in seconds
assign   around_time              900    # in seconds, avoid obstacles

assign   notdefined               100000


# the elevation parameters

assign downhillcost 0
assign downhillcutoff 1.5
assign uphillcost 0
assign uphillcutoff 1.5

---context:way   # following code refers to way-tags

    
# to use this an update for lokkup.dat is recommended

    
assign not_used
    switch boat=no                        true
    switch boat=private                   true
    switch motorboat=no                   true
    switch motorboat=private              true
    switch access=private                 true
    switch access=no                      true
	switch disused=yes                    true
	switch disused:waterway=yes           true
    false

# https://wiki.openstreetmap.org/wiki/Key:CEMT

assign is_waterway
    switch CEMT=0|I|II|III|IV|V|Va|Vb|VI|VIa|VIb|VIc|VII true   # make sure to drop classes you can't use
    switch waterway=river|canal|fairway|safe_water|lock  true
    switch seamark:type=fairway|recommended_track        true
    switch motorboat=yes                                 true
    switch sailboat=yes                                  true
	switch ship=yes                                      true
#    switch boat=yes|permissive|designated                true   # belongs to your boat 
    false

assign maxspeed_pre 
    switch maxspeed=5  5
    switch maxspeed=6  6
    switch maxspeed=7  7
    switch maxspeed=8  8
    switch maxspeed=9  9
    switch maxspeed=12 12
    switch maxspeed=15 15
    switch maxspeed=16 16
    switch maxspeed=20 20
    switch maxspeed=25 25
    switch CEMT=0                   6
    switch CEMT=I|II|III|IV         10
    switch CEMT=V|Va|Vb             12
    switch CEMT=VI|VIa|VIb|VIc|VII  15
    switch seamark:type=lock_basin  1
    switch waterway=lock            1
    switch lock=yes                 1
    speed_normal

assign maxspeed
	min maxspeed_pre speed_normal
   
assign turncost        0

assign initialcost     0


assign costfactor
    switch and is_waterway not_used         notdefined
    switch waterway=lock                    10
    switch seamark:type=lock_basin          10    
    switch obstacle=lock	                10
    switch is_waterway                      1
    notdefined
        


---context:node  # following code refers to node tags


assign initialcost 
    switch waterway=lock_gate|lock  waiting_lock
    0
    
    
